apply plugin: 'java'
apply plugin: 'com.novoda.bintray-release'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile group: 'com.google.code.findbugs', name: 'jsr305', version:'3.0.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version:'1.3'
    testCompile group: 'junit', name: 'junit', version:'4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    failOnError = false
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publish {
    bintrayUser = 'bkromhout'
    bintrayKey = BINTRAY_KEY
    userOrg = 'bkromhout'
    groupId = 'com.github.bkromhout'
    artifactId = 'java-diff-utils'
    version = '2.1.1'
    desc = 'The java-diff-utils library is for computing diffs, applying patches, generation side-by-side view in Java.'
    website = 'https://github.com/bkromhout/java-diff-utils'
    licences = ['Apache-2.0']
}